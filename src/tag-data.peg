{
  /**
   * @example
   * // returns ['ruby', 'python', 'perl']
   * mergeSeparatedValues('ruby', [[' ', 'python'], ['   ', 'perl']]);
   * @returns {Array}
   */
  function mergeSeparatedValues(value, variadicArray) {
    var i, result = [value];
    for (i = 0; i < variadicArray.length; i++) {
      result.push(variadicArray[i][1]);
    }
    return result;
  }
}

Start
  = TagData

TagData "tag data"
  = value:TagDatum variadicArray:($TagSeparater TagDatum)* {
    return mergeSeparatedValues(value, variadicArray);
  }
  / "" {
    return [];
  }

TagSeparater
  = " "+

TagDatum "name and versions"
  = name:$Name versions:(":" Versions)? {
    return { name: name, versions: versions ? versions[1] : [] };
  }

Name
  = [^<>;:"' ]+

Versions
  = value:Version variadicArray:($VersionSeparater Version)* {
    return mergeSeparatedValues(value, variadicArray);
  }

Version
  = version:$([^,\\ ] / "\\ " / "\\\\")+ {
    return version.replace(/\\ /g, " ").replace(/\\\\/g, '\\');
  }

VersionSeparater
  = ","
